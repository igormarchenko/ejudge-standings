plugins {
    id 'com.craigburke.bower-installer' version '2.5.1'
    id "com.eriwen.gradle.js" version "2.14.1"
    id "com.eriwen.gradle.css" version "2.14.0"
}

group 'org.ssu'
version '1.1-SNAPSHOT'

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'css'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

bower {
    installBase = '/bower_components'

    'angular'('>=1.5.8') {
        source 'angular.js'
    }

    'angular-sanitize'('https://github.com/angular/bower-angular-sanitize.git')
    'angular-ui-select'('https://github.com/angular-ui/ui-select.git')
    'ui-router'('0.2.x')

    'angular-route'('>=1.5.8') {
        source 'angular-route.js' >> 'angular-route.js'
    }

    'bootstrap'('3.3.x') {
        source 'dist/css/*.min.css' >> 'styles/'
        source 'dist/fonts/**' >> 'fonts/'
        excludes 'jquery'
    }
    'datatables.net'('1.10.12')
    'datatables.net-dt'('1.10.12')
    'angular-datatables'('0.5.5')
    'sprintf.js'("https://github.com/alexei/sprintf.js.git")
    'ngInfiniteScroll'('1.0.0')
}

def scriptFiles = ['bower_components/jquery/dist/jquery.min.js',
//                   'bower_components/bootstrap/dist/js/bootstrap.min.js',
                   'bower_components/angular/angular.min.js',
//                   'bower_components/angular-bootstrap/ui-bootstrap.js',
                   'bower_components/ngInfiniteScroll/build/ng-infinite-scroll.min.js',
                   'bower_components/angular-sanitize/angular-sanitize.min.js',
                   'bower_components/angular-route/angular-route.min.js',

                   'bower_components/angular-ui-select/dist/select.min.js',
                   'bower_components/ui-router/release/angular-ui-router.js',
                   'bower_components/sprintf.js/dist/sprintf.min.js',
                   'bower_components/datatables.net/js/jquery.dataTables.min.js',
                   'bower_components/angular-datatables/dist/angular-datatables.min.js']

def styleFiles = ['bower_components/angular-ui-select/dist/select.min.css',
                  'bower_components/bootstrap/dist/css/bootstrap.min.css',
                  'bower_components/datatables.net-dt/css/jquery.dataTables.min.css',
                  'bower_components/angular-datatables/dist/css/angular-datatables.min.css']

task copyBootstrapFonts(type: Copy) {
    from 'bower_components/bootstrap/dist/fonts'
    into 'src/main/webapp/static/fonts'
}


task copyDatatablesImages(type: Copy) {
    from 'bower_components/datatables.net-dt/images'
    into 'src/main/webapp/static/images'
}
combineJs {
    encoding = "UTF-8"
    source = scriptFiles
    dest = file("src/main/webapp/static/script.js")
}

minifyJs {
    source = combineJs
    dest = file("src/main/webapp/static/script.js")
    sourceMap = file("all.sourcemap.json")
    closure {
        warningLevel = 'QUIET'
    }
}

combineCss {
    source = styleFiles
    dest = "${buildDir}/all.css"
}

minifyCss {
    source = combineCss
    dest = file("src/main/webapp/static/style.css")
    yuicompressor {
        lineBreakPos = -1
    }
}

task installPlugins() {
    dependsOn.add(bowerRefresh)
    dependsOn.add(copyBootstrapFonts)
    dependsOn.add(copyDatatablesImages)
    dependsOn.add(combineJs)
    dependsOn.add(minifyCss)
}

assemble.dependsOn.add(installPlugins)

dependencies {


    def springVersion = '4.3.2.RELEASE'
    def jacksonVersion = '2.8.8'

    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.7.21'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testCompile group: 'junit', name: 'junit', version: '4.11'

    compile group: 'org.springframework', name: 'spring-webmvc', version: springVersion
    compile group: 'org.springframework', name: 'spring-core', version: springVersion
    compile group: 'org.springframework', name: 'spring-websocket', version: springVersion
    compile group: 'org.springframework', name: 'spring-messaging', version: springVersion

    compile group: 'javax.websocket', name: 'javax.websocket-api', version: '1.1'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion

    compile group: 'org.springframework', name: 'spring-jdbc', version: springVersion
    compile group: 'org.springframework', name: 'spring-tx', version: springVersion
    compile group: 'org.springframework', name: 'spring-orm', version: springVersion
    compile group: 'org.postgresql', name: 'postgresql', version: '9.4.1211.jre7'
    compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.2.Final'
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: '5.2.2.Final'
    compile group: 'org.hibernate.javax.persistence', name: 'hibernate-jpa-2.1-api', version: '1.0.0.Final'
    compile group: 'javax.persistence', name: 'persistence-api', version: '1.0.2'
    compile group: 'org.springframework.data', name: 'spring-data-jpa', version: '1.10.2.RELEASE'
    compile group: 'org.liquibase', name: 'liquibase-core', version: '3.5.2'
    compile group: 'org.apache.tomcat', name: 'tomcat-dbcp', version: '9.0.0.M10'


    compile group: 'javax.servlet', name: 'jstl', version: '1.2'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

    compile group: 'commons-net', name: 'commons-net', version: '3.5'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

    compile group: 'org.springframework.security', name: 'spring-security-core', version: '4.1.3.RELEASE'
    compile group: 'org.springframework.security', name: 'spring-security-config', version: '4.1.3.RELEASE'
    compile group: 'org.springframework.security', name: 'spring-security-web', version: '4.1.3.RELEASE'
    compile group: 'org.springframework.security', name: 'spring-security-taglibs', version: '4.1.3.RELEASE'

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'net.sourceforge.pjl-comp-filter', name: 'pjl-comp-filter', version: '1.7'

}
